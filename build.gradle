repositories {
	repositories {
		mavenLocal()
		mavenCentral()
	}
}

description = 'The Gradle plug-in for transforming Markdown files into HTML files with approach of http://strapdownjs.com/.'
group = 'com.github.gradle-plugins'
version = '0.2-SNAPSHOT'


apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

buildDir = '.build'
if (!hasProperty('bintrayUserName')) ext.bintrayUserName = ''
if (!hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

sourceCompatibility = 1.6
targetCompatibility = 1.6

configurations {
	deployerJars
}

dependencies {
	compile gradleApi()
	compile localGroovy()
	//compile 'commons-lang:commons-lang:2.6'
	//compile 'org.apache.commons:commons-lang3:3.1'
	//testCompile 'commons-io:commons-io:2.4'
	testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'

	// for publishing to Bintray
	deployerJars 'org.apache.maven.wagon:wagon-webdav:1.0-beta-2'
}

task wrapper(type: Wrapper) {
	description = 'Gradle wrapper task'
	gradleVersion = '1.6'
}

install.dependsOn check

uploadArchives {
	dependsOn check
	repositories.mavenDeployer {
		configuration = configurations.deployerJars
		repository(url: 'https://api.bintray.com/maven/dmaslakov/gradle-plugins/gradle-strapdownjs-plugin') {
			authentication userName: bintrayUserName, password: bintrayApiKey
		}
	}
}

[install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.project {
	artifactId 'gradle-strapdownjs-plugin'
	version = project.version
	groupId = project.group
	description = project.description

	url 'https://github.com/dmaslakov/gradle-strapdownjs-plugin'
	inceptionYear '2013'
	licenses {
		license {
			name 'The MIT License'
			url 'https://raw.github.com/dmaslakov/gradle-strapdownjs-plugin/master/LICENSE'
			distribution 'repo'
		}
	}
	scm {
		connection 'scm:git:https://github.com/dmaslakov/gradle-strapdownjs-plugin.git'
		url 'https://github.com/dmaslakov/gradle-strapdownjs-plugin'
	}
	developers {
		developer {
			id 'dmaslakov'
			name 'Dmitry Maslakov'
			roles { role 'developer' }
			timezone 'Asia/Novosibirsk (UTC+07:00)'
		}
	}
	issueManagement {
		system 'GitHub'
		url 'https://github.com/dmaslakov/gradle-strapdownjs-plugin/issues'
	}
}
